%{
#include <iostream>
#include <string>
#include <map>
#include "src/variables.hpp"
#include "src/statement.hpp"

#define YYDEBUG 1

void yyerror(std::string message) {
    std::cerr << message << std::endl;
    exit(EXIT_FAILURE);
}

extern int yylex();
extern int line_counter;

std::map<std::string, Variable*> variables_table;
%}

%token begin_token end_token true_token false_token
%token <str_type> id_token string_token
%token <num_type> num_token

%type <var_type> E
%type <array_type> Array
%type <object_type> ObjectArray
%type <pair_type> ObjectStatement
%type <state_type> Block Statement
%type <statements_type> StatementArray

%union {
    Variable* var_type;
    Statement* state_type;
    std::vector<Variable*>* array_type;
    std::vector<Statement*>* statements_type;
    std::map<std::string, Variable*>* object_type; 
    std::pair<std::string, Variable*>* pair_type;
    std::string* str_type;
    float num_type;
}

%%

Program: Block {
    $1->run();
}
;

Block: begin_token StatementArray end_token {
    $$ = new Block(*$2);

    delete $2;
}
;

StatementArray: StatementArray Statement ';'{
    $$ = $1;
    $$->push_back($2);
}
| Statement ';' {
    $$ = new std::vector<Statement*>();
    $$->push_back($1);
}
;

Statement: id_token '=' E {
    $$ = new Assignment(*$1, $3);
    
    delete $1;
}
;

E: string_token {
    $$ = new String(*$1);
    
    delete $1;
}
| num_token {
    $$ = new Number($1);
}
| true_token {
    $$ = new Boolean(true);
}
| false_token {
    $$ = new Boolean(false);
}
| '{' ObjectArray '}' {
    $$ = new Object(*$2);

    delete $2;
}
| '[' Array ']' {
    $$ = new Array(*$2);

    delete $2;
}
;

Array: Array ',' E {
    $$ = $1;
    $$->push_back($3);
}
| E {
    $$ = new std::vector<Variable*>();
    $$->push_back($1);
}
;

ObjectArray: ObjectStatement ',' ObjectArray {
    $3->insert(*$1);
    $$ = $3;

    delete $1;
}
| ObjectStatement {
    $$ = new std::map<std::string, Variable*>();
    $$->insert(*$1);

    delete $1;
}
;

ObjectStatement: id_token ':' E {
    $$ = new std::pair<std::string, Variable*>(*$1, $3);

    delete $1;
}
;

%%

extern FILE* yyout;
extern FILE* yyin;

/* Program must be called with input and output filenames. */
int main(int argc, char** argv)
{
    // yydebug = 1;

    if(argc < 3)
        yyerror("Input or output filename missing.");

    if((yyin = fopen(argv[1], "r")) == NULL)
        yyerror("Opening input file failed.");

    if((yyout = fopen(argv[2], "w")) == NULL)
        yyerror("Opening output file failed.");
    
    yyparse();

    return 0;
}